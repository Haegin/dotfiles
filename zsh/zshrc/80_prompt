# zshrc/80_prompt
#
# Formats the shell prompt
#
# Copyright © 1994–2008 martin f. krafft <madduck@madduck.net>
# Released under the terms of the Artistic Licence 2.0
#
# Source repository: git://git.madduck.net/etc/zsh.git

# Allow for functions in the prompt.
setopt PROMPT_SUBST

function rvm_ruby_prompt {
    if (declare -f rvm > /dev/null) {
        command -v rvm-prompt >/dev/null 2>&1
        if [[ $? -eq 0 ]] then
            #ruby -v | sed 's/\([^(]*\).*/\1/'
            rvm-prompt
        fi
    }
}

# HJM: replaced with new version of my prompt

function prompt {
    if [ $USER = 'harry' ]; then
        hostprompt="%{$fg[blue]%}%m"
    else
        hostprompt="%{$fg[cyan]%}%m"
    fi

    maxlength=$(( ${COLUMNS} / 8 ))
    path_prompt=$(print -P %~) # get the current working dir from %~
    if (( ${#path_prompt} > ${maxlength} )); then
        p_tchars='../'
        p_done=${path_prompt}
        for (( i=1 ; ; ++i )); do
            p_temp=$(print -P %${i}~)
            if (( ( ${#p_temp} + ${#p_tchars} ) < ${maxlength} )); then
                p_done=${p_temp}
            else
                break
            fi
        done
        path_prompt=${p_tchars}${p_done}
    fi

    # See if we can use colors.
    autoload colors zsh/terminfo
    if [[ "$terminfo[colors]" -ge 8 ]]; then
        colors
    fi

    # Add in something for virtualenv directories
    virtual_env_prompt=""
    if [[ -z "$VIRTUAL_ENV_DISABLE_PROMPT" && -n $VIRTUAL_ENV ]] ; then
        if [ "`basename \"$VIRTUAL_ENV\"`" = "__" ] ; then
            # special case for Aspen magic directories
            # see http://www.zetadev.com/software/aspen/
            #PS1="[`basename \`dirname \"$VIRTUAL_ENV\"\``] $PS1"
            virtual_env_prompt="[`basename \`dirname \"$VIRTUAL_ENV\"\``]"
        else
            virtual_env_prompt="(`basename \"$VIRTUAL_ENV\"`)"
        fi
    fi
    export PROMPT="%{%(!.$fg[red].$fg[green])%}%n%{$fg[default]%}@${hostprompt}%{$fg[default]%}:%{$fg[yellow]%}${path_prompt}${vcs_info_msg_0_}%{$fg[green]%}${virtual_env_prompt}%{$fg[red]%}%(?..[%?])%{$fg[default]%}%# "
    TIME="[%{$fg[cyan]%}%t%{$fg[default]%}]"
    # Rubies are red, and my rprompt is too
    RUBY_PROMPT='%{$fg[red]%}$(rvm_ruby_prompt)%{$reset_color%}%'
    export RPROMPT=${RUBY_PROMPT:-TIME}
}

precmd_functions+=prompt

# vim:ft=zsh
