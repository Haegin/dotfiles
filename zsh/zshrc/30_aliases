# zshrc/30_aliases
#
# Defines command shortcuts
#
# Copyright © 1994–2008 martin f. krafft <madduck@madduck.net>
# Released under the terms of the Artistic Licence 2.0
#
# Source repository: git://git.madduck.net/etc/zsh.git
#

function trace() { eval "set -x; $@" }

alias mv='nocorrect mv'
alias cp='nocorrect cp'
alias ln='nocorrect ln'
alias mkdir='nocorrect mkdir'
alias rm='nocorrect rm'

alias find='noglob find'
alias w3m='noglob w3m -no-mouse'
alias links='noglob links'
alias wb='noglob www-browser'
alias wget='noglob wget'
alias curl='noglob curl'
alias apt-cache='noglob apt-cache'
alias locate='noglob locate'

local _USER_AGENT="Mozilla/5.0 (X11; U; Linux i686; en-US; rv:1.8.1.14) Gecko/20080404 Iceweasel/2.0.0.14 (Debian-2.0.0.14-2)"
alias swget="noglob wget --user-agent='$USER_AGENT'"

typeset -a _ls_opts; _ls_opts=(-bC --color=auto)
command ls --group-directories-first >/dev/null 2>&1 \
  && _ls_opts+=--group-directories-first
# I prefer POSIX collation for ls
alias ls="LC_COLLATE=POSIX ls $_ls_opts"

alias l='ls --time-style=+%Y%m%d.%H%M -gG'
alias ll='ls -l'
alias la='ls -al'
alias lh='ls -lh'
alias lt='ls -lt'
alias lah='ls -hal'
alias lrt='ls -lrt'
alias lat='ls -lat'
alias lart='ls -lart'
# List only directories and symbolic links that point to directories
alias lsd='ll -d *(-/DN)'

# HJM: some vim aliases
alias vmore='vim -u ~/.vimrc-more'
alias vil='vi *(.om[1]^D)'

# HJM: Dvorak aliases
alias aoeu='setxkbmap gb'
alias asdf='setxkbmap gb dvorak && xmodmap ~/.Xmodmap'

alias lscontext=ls
cd () { __CHPWD_RUN_FIRST=lscontext && builtin cd "$@" && unset __CHPWD_RUN_FIRST }
#_cd_l() { ll }
#chpwd_functions+=_cd_l

# #183394
alias w='w|cat'

alias egrep='egrep --color=auto -d skip'
alias fgrep='fgrep --color=auto -d skip'
alias grep='grep --color=auto -d skip'

alias mmv='noglob zmv -W'
alias zcp='zmv -C'
alias zln='zmv -L'
alias zgmv='zmv -p git-mv'

alias cal='cal -3'
alias ftp=ncftp

# handy documentation lookup on Debian
# from http://www.michael-prokop.at/computer/config/.zshrc
doc() { cd /usr/share/doc/$1 }
compdef '_files -W /usr/share/doc -/' doc

alias giti='git init && git add . && git commit -minitial\ checkin && git tag orig'

if whence mtail >/dev/null; then
  alias t=mtail
elif whence tailf >/dev/null; then
  alias t=tailf
else
  alias t='tail -f'
fi
compdef _files t

function ltail() { t ~log/$1 }
compdef '_files -W ~log' ltail

function lless() { sensible-pager ~log/$1 }
compdef '_files -W ~log' lless

alias tsl='ltail syslog'
alias lsl='lless syslog'
alias tml='ltail mail.log'
alias lml='lless mail.log'
alias tal='ltail auth.log'
alias lal='lless auth.log'
alias tdl='ltail daemon.log'
alias ldl='lless daemon.log'
alias tkl='ltail kern.log'
alias lkl='lless kern.log'

function lgrep() {
  local pattern="$1"; shift
  eval egrep "'$pattern'" "~log/${(j: ~log/:)@}"
}

alias px='ps afux'
alias mps='ps -ao user,pcpu,start,command'

function of() { lsof -np "$1" }
compdef _pids of

function psgrep() { ps aux | grep "[${1[1]}]${1[2,-1]}" }

alias bofh='fortune bofh-excuses'

autoload -U baseconv
alias b2d='baseconv 2 10'
alias d2b='baseconv 10 2'
alias d2h='baseconv 10 16'
alias h2d='baseconv 16 10'
alias d2o='baseconv 10 8'
alias d2o='baseconv 10 8'
alias h2b='baseconv 16 2'
alias b2h='baseconv 2 16'

alias sendmail=/usr/sbin/sendmail

_d() { dict $* }
d() { _d $* | sensible-pager }
the() { d -d moby-thesaurus $* }
typeset -A _LANGUAGES
_LANGUAGES[en]=eng
_LANGUAGES[de]=deu
_LANGUAGES[es]=spa
_LANGUAGES[fr]=fra
_LANGUAGES[it]=ita
_LANGUAGES[tn]=lat
for l0 in ${(k)_LANGUAGES}; do
  for l1 in ${(k)_LANGUAGES}; do
    eval "function _${l0}-${l1}() { _d -d fd-${_LANGUAGES[$l0]}-${_LANGUAGES[$l1]} \$* }"
    eval "function ${l0}-${l1}() { d -d fd-${_LANGUAGES[$l0]}-${_LANGUAGES[$l1]} \$* }"
  done
done
unset '_LANGUAGES[en]'
for l in ${(k)_LANGUAGES}; do
  eval "function ${l}() { (_${l}-en \$*; _en-${l} \$*) | more }"
done
unset _LANGUAGES

alias dpkg-buildpackage="dpkg-buildpackage -k$DEBKEYID -rfakeroot"

uc() { unicode --max=0 --colour=off "$@" | sensible-pager }

alias ipr='ip r'
alias ipa='ip a'
alias ipl='ip l'

if whence devtodo >/dev/null; then
  _devtodo_on_chpwd() {
    local out; out="$(devtodo)"
    [[ -n $out ]] && print "todo:\n$out"
  }
  [[ -z ${(M)chpwd_functions:#_devtodo_on_chpwd} ]] && chpwd_functions+=_devtodo_on_chpwd
fi

# MPD alias
if [ `which mpd > /dev/null 2>&1` ]; then
	alias musicstatus='mpc status | osd_cat --pos=middle --align=center -s=1 -'
fi

# Tolkien specific
if [ `hostname` = "tolkien" ]; then
	alias headmplayer='mplayer -ao alsa:device=plughw=1.0'
fi

# IRC
alias irc='ssh hobb -t "screen -D -RR -S irc irssi"'

# Wake on Lan
alias waketolkien='wakeonlan 00:22:15:45:f9:8a'

# Note taking
# http://jasonwryan.com/post/1203000683/awk-notes
n() { $EDITOR ~/notes/"$*".txt }
nls () { tree -CR --noreport ~/.notes | awk '{ if ((NR > 1) gsub(/.txt/,"")); if (NF==1) print $1; else if (NF==2) print $2; else if (NF==3) printf " %s\n", $3 }' ;}

# vim:ft=zsh
