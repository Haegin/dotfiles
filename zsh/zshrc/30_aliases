# zshrc/30_aliases
#
# Defines command shortcuts
#
# Copyright © 1994–2008 martin f. krafft <madduck@madduck.net>
# Released under the terms of the Artistic Licence 2.0
#
# Source repository: git://git.madduck.net/etc/zsh.git
#

function trace() { eval "set -x; $@" }

alias mv='nocorrect mv'
alias cp='nocorrect cp'
alias ln='nocorrect ln'
alias mkdir='nocorrect mkdir'
alias rm='nocorrect rm'

alias find='noglob find'
alias w3m='noglob w3m -no-mouse'
alias links='noglob links'
alias wb='noglob www-browser'
alias wget='noglob wget'
alias curl='noglob curl'
alias apt-cache='noglob apt-cache'
alias locate='noglob locate'

local _USER_AGENT="Mozilla/5.0 (X11; U; Linux i686; en-US; rv:1.8.1.14) Gecko/20080404 Iceweasel/2.0.0.14 (Debian-2.0.0.14-2)"
alias swget="noglob wget --user-agent='$USER_AGENT'"

typeset -a _ls_opts
# If we're using bsd style coreutils --color isn't supported and -C is slightly
# different
[[ $_coreutils_type = 'BSD' ]] && _ls_opts=(-bG) || _ls_opts=(-bC --color=auto)
command ls --group-directories-first >/dev/null 2>&1 \
  && _ls_opts+=--group-directories-first
# I prefer POSIX collation for ls
alias ls="LC_COLLATE=POSIX ls $_ls_opts"

alias l='ls --time-style=+%Y%m%d.%H%M -gG'
alias ll='ls -l'
alias la='ls -al'
alias lh='ls -lh'
alias lt='ls -lt'
alias lah='ls -hal'
alias lrt='ls -lrt'
alias lat='ls -lat'
alias lart='ls -lart'
# List only directories and symbolic links that point to directories
alias lsd='ll -d *(-/DN)'

# Better programs if they're installed
if [[ -x $(which dfc) ]]; then
  if [[ $(hostname) -eq 'odin' ]]; then
    alias df='dfc -f | grep -v export'
  else
    alias df='dfc'
  fi
fi
if [[ -x $(which htop) ]]; then
  alias top='htop'
fi

alias lscontext=ls
#cd () { __CHPWD_RUN_FIRST=lscontext && builtin cd "$@" && unset __CHPWD_RUN_FIRST }
_cd_l() { lscontext }
chpwd_functions+=_cd_l

alias egrep='egrep --color=auto -d skip'
alias fgrep='fgrep --color=auto -d skip'
alias grep='grep --color=auto -d skip'

if whence mtail >/dev/null; then
  alias t=mtail
elif whence tailf >/dev/null; then
  alias t=tailf
else
  alias t='tail -f'
fi
compdef _files t

function ltail() { t ~log/$1 }
compdef '_files -W ~log' ltail

function lless() { sensible-pager ~log/$1 }
compdef '_files -W ~log' lless

alias px='ps afux'
alias mps='ps -ao user,pcpu,start,command'

function psgrep() { ps aux | grep "[${1[1]}]${1[2,-1]}" }

# IRC & screen
alias \#="tmux -u2 attach -d"
alias mux=tmuxinator

# Git spelling mistakes and shortcuts
alias it=git
alias gap='git add -p'
alias gs='git status -s'

# Neovim and editor aliases
alias nv=nvim
function e() { $EDITOR "$@" }

# vim:ft=zsh
