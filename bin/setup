#!/bin/zsh
setopt extendedglob

DOTDIR="${HOME}/.dotfiles"

cd "$DOTDIR"

OS="$(uname -s)"

function link_dotfiles() {
  # Only want to symlink the files, not the utility scripts
  echo "Linking files into ${HOME} from ${DOTDIR}:"
  for item in *~(bin|utils|docs|profiles); do
    if [ ! -e "${HOME}/.${item}" ]; then
      echo "- .${item}"
      ln -s "${DOTDIR}/${item}" "${HOME}/.${item}"
    fi
  done
}

function setup_history() {
  echo "Making history"
  mkdir -p "${ZVARDIR:-${HOME}/.var/zsh}"
}

function setup_base16_shell() {
  local base16_dir="$HOME/.config/base16-shell"
  if [[ ! -d "$base16_dir" ]]; then
    echo "Installing base16-shell for terminal color schemes"
    git clone https://github.com/chriskempson/base16-shell "$base16_dir"
  fi
}

function setup_antidote() {
  # Skip if installed via homebrew
  if [[ -f /opt/homebrew/opt/antidote/share/antidote/antidote.zsh ]]; then
    return
  fi
  if [[ ! -d "$HOME/.antidote" ]]; then
    echo "Installing antidote plugin manager"
    git clone --depth=1 https://github.com/mattmc3/antidote.git "$HOME/.antidote"
  fi
}

function run_custom_setup() {
  if [[ -x ./profiles/$USER ]]; then
    profile=$USER
  else
    profile=$(ls profiles | fzf --height=10% --prompt="Select a profile to further customise your install or press Ctrl+C to cancel > ")
  fi
  if [[ -v profile ]]; then
    echo "Running ${profile}'s custom setup script'"
    if [[ -d ./profiles/$profile ]]; then
      ./profiles/$profile/setup
    else
      ./profiles/$profile
    fi
  fi
}

if [[ -d "$HOME/.dotfiles" ]]; then
  echo "Setting up your machine...\n"
  # Ask for the administrator password upfront
  sudo -v
  # Keep-alive: update existing `sudo` time stamp until setup has finished
  while true; do sudo -n true; sleep 60; kill -0 "$$" || exit; done 2>/dev/null &
  link_dotfiles
  setup_history
  setup_antidote
  setup_base16_shell
  if [[ -e "${DOTDIR}/bin/setup.${OS}" ]]; then
    echo "Running OS specific setup"
    "${DOTDIR}/bin/setup.${OS}"
  fi
  run_custom_setup
  echo "\nFinished!"
else
  echo "Dotfiles don't seem to be correctly set up."
  echo "Ensure you've cloned github.com/haegin/dotfiles into $HOME/.dotfiles."
fi
