#!/bin/zsh
setopt extendedglob

DOTDIR="${HOME}/.dotfiles"

cd "$DOTDIR"

OS="$(uname -s)"

function setup_submodules() {
  # Set up the submodules now we're done so vim plugins are all working
  echo "Setting up the git submodules."
  git submodule init
  git submodule update
}

function link_dotfiles() {
  # Only want to symlink the files, not the utility scripts
  echo "Linking files into ${HOME} from ${DOTDIR}:"
  for item in *~(bin|utils|docs|profiles); do
    if [ ! -e "${HOME}/.${item}" ]; then
      echo "- .${item}"
      ln -s "${DOTDIR}/${item}" "${HOME}/.${item}"
    fi
  done
}

function setup_history() {
  echo "Making history"
  mkdir -p "${ZVARDIR:-${HOME}/.var/zsh}"
}

function install_vscode_extensions() {
  echo "Setting up VSCode"
  code --install-extension 'dbaeumer.vscode-eslint'
  code --install-extension 'ecmel.vscode-html-css'
  code --install-extension 'esbenp.prettier-vscode'
  code --install-extension 'dracula-theme.theme-dracula'
}

function install_js() {
  echo "Installing Node using ASDF"
  if [[ -e $HOME/.asdf ]]; then
    source $HOME/.asdf/asdf.sh
    asdf plugin-add nodejs
    bash ~/.asdf/plugins/nodejs/bin/import-release-team-keyring
    asdf install nodejs 8.12.0
  else
    echo 'Could not install JS as ASDF is not installed.'
  fi
}

function run_custom_setup() {
  if [[ -x ./profiles/$USER ]]; then
    profile=$USER
  else
    profile=$(ls profiles | fzf --height=10% --prompt="Select a profile to further customise your install or press Ctrl+C to cancel > ")
  fi
  if [[ -v profile ]]; then
    echo "Running ${profile}'s custom setup script'"
    if [[ -d ./profiles/$profile ]]; then
      ./profiles/$profile/setup
    else
      ./profiles/$profile
    fi
  fi
}

if [[ -d "$HOME/.dotfiles" ]]; then
  echo "Setting up your machine...\n"
  # Ask for the administrator password upfront
  sudo -v
  # Keep-alive: update existing `sudo` time stamp until setup has finished
  while true; do sudo -n true; sleep 60; kill -0 "$$" || exit; done 2>/dev/null &
  setup_submodules
  link_dotfiles
  setup_history
  if [[ -e "${DOTDIR}/bin/setup.${OS}" ]]; then
    echo "Running OS specific setup"
    "${DOTDIR}/bin/setup.${OS}"
  fi
  install_vscode_extensions
  install_js
  run_custom_setup
  echo "\nFinished!"
else
  echo "Dotfiles don't seem to be correctly set up."
  echo "Ensure you've cloned github.com/HighlineBETA/ptah into $HOME/.dotfiles."
fi
